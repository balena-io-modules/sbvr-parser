Rule ::=
	Modifier
	RuleBody
	'.'?
RuleBody ::=
	(	Quantifier
		TermEntity
	|	'the'
		(	referenced-term
		|	name
		)
		('(' vocabulary ')')?
	|	Number
	|	'"' Text '"'
	)
	(	is-atomic-formulation
	|	verb
		(	is-atomic-formulation
		|	RuleBody
		)
	)

TermEntity ::=
	term ('(' vocabulary ')')?
	(	ClosedProjection
		','?
	)?

Modifier ::=
	(	'It is obligatory that'
	|	'It is necessary that'
	|	'It is prohibited that'
	|	'It is impossible that'
	|	'It is not possible that'
	|	'It is possible that'
	|	'It is permitted that'
	)
Quantifier ::=
		(	'each'
		|	'a'
		|	'an'
		|	'some'
		)
	|	(
		|	'at most'
		|	'at least'
			(Number 'and at most')?
		|	'more than'
		|	'exactly'
		)
		Number
ClosedProjection ::=
	'that'
	(	verb
		(	is-atomic-formulation
		|	RuleBody
		)
	|	RuleBody
	)

Number ::=
		'one'
	|	[0-9]+

Attribute ::=
		Definition
	|	GuidanceType
	|	Necessity

Definition ::=
		'the'?
		TermEntity
		'.'?
	|	value
		(	','
			value
		)*
		(	(	','?
				'or'
				value
			)+
		|	no-comma-values
		)

Necessity ::=
	RuleBody
	'.'?

GuidanceType ::=
		'operative business rule'
	|	'structural business rule'
	|	'advice of permission'
	|	'advice of possibility'
	|	'advice of optionality'
	|	'advice of contingency'