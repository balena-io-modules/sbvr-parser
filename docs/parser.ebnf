Rule ::=
	Modifier
	RuleBody JunctionRepeat*
	'.'?
RuleBody ::=
	(	Quantifier
		TermEntity
	|	'the'
		(	referenced-term
		|	name
		)
		('(' vocabulary ')')?
	|	Number
	|	'"' Text '"'
	)
	(	is-atomic-formulation
	|	VerbContinuation JunctionRepeat*
	)
VerbContinuation ::=
	verb
	(	is-atomic-formulation
	|	RuleBody JunctionRepeat*
	)

TermEntity ::=
	term ('(' vocabulary ')')?
	(	ClosedProjection
		','?
	)?

JunctionRepeat ::=
	(Disjunction | Conjunction)
	repeat-previous-rule

Modifier ::=
	(	'It is obligatory that'
	|	'It is necessary that'
	|	'It is prohibited that'
	|	'It is impossible that'
	|	'It is not possible that'
	|	'It is possible that'
	|	'It is permitted that'
	)
Quantifier ::=
		(	'each'
		|	'a'
		|	'an'
		|	'some'
		)
	|	(
		|	'at most'
		|	'at least'
			(Number 'and at most')?
		|	'more than'
		|	'exactly'
		)
		Number
ClosedProjection ::=
	'that'
	(	VerbContinuation JunctionRepeat
	|	RuleBody JunctionRepeat*
	)

Disjunction ::=
	'or'

Conjunction ::=
	'and'

Number ::=
		'one'
	|	[0-9]+

Attribute ::=
		Definition
	|	GuidanceType
	|	Necessity

Definition ::=
		'the'?
		TermEntity
		'.'?
	|	value
		(	','
			value
		)*
		(	(	','?
				'or'
				value
			)+
		|	no-comma-values
		)

Necessity ::=
	RuleBody
	'.'?

GuidanceType ::=
		'operative business rule'
	|	'structural business rule'
	|	'advice of permission'
	|	'advice of possibility'
	|	'advice of optionality'
	|	'advice of contingency'